program sudoku2;

var
	sdk:array [0..8,0..8] of integer;
	i,j:integer;

{esta parte que sigue es sólo para probar si funciona, lo de archivos despues lo agrego}

procedure llenarTablero;

begin
	for i:=0 to 8 do
	begin
		for j:=0 to 8 do
		begin
			sdk[i,j]:=0;
		end;		
	end;
	
	sdk[0,0] := 6; sdk[0,2] := 7; sdk[0,6] := 9;
	sdk[0,8] := 8; sdk[1,2] := 3; sdk[1,3] := 9; 
	sdk[1,5] := 1; sdk[1,6] := 6; sdk[2,1] := 4; 
	sdk[2,3] := 8; sdk[2,6] := 5; sdk[3,1] := 1; 
	sdk[3,2] := 5; sdk[3,3] := 7; sdk[3,4] := 2; 
	sdk[3,6] := 8; sdk[3,7] := 6; sdk[4,4] := 4; 
	sdk[4,7] := 5; sdk[5,1] := 7; sdk[5,2] := 9; 
	sdk[5,4] := 6; sdk[6,3] := 6; sdk[6,4] := 9; 
	sdk[6,7] := 4; sdk[7,5] := 3; sdk[8,2] := 2;
	sdk[8,3] := 5; sdk[8,4] := 1; sdk[8,8] := 6;
	
end;



procedure mostrarTablero;

begin
  for i:=0 to 8 do
    begin
		for j:=0 to 8 do
		begin
			write(sdk[i,j],' ');
		end;
		writeln
    end;
    writeln
end;


	
function repetido (num,x,y:integer):boolean;

var
	a,b:integer;

begin
	repetido:=false;
	for a:=0 to 8 do
	begin
		if sdk[x,a] = num then
		begin
			exit (true);
		end;
	end;
	
	for a:=0 to 8 do
	begin
		if sdk[a,y] = num then
		begin
			exit (true);
		end;
	end;	
	
	for a:=0 to 2 do
	begin
		for b:=0 to 2 do
		begin
			if sdk[((x div 3) * 3)+a,((y div 3) * 3)+b] = num then
			begin
				exit (true);
			end;
		end;
	end;
end;


{este procedimiento de abajo recorre la matriz, cunado encuentra un cero prueba numeros del 1 al 9, si cumple con la condición escribe ese número encima del 
cero y llama al procedimiento de nuevo. Cuando ninguno de los nueve números cumple con la condición sale de la llamada recursiva; ahora está de nuevo en la 
última celda donde había puesto un número que cumplía la condición pero conducía a una callejón sin salida, entonces escribe un cero encima de ese número 
y sigue el bucle con otro número distinto hasta que encuentre el que no lleva a un callejón sin salida. Cuando ningun número se puede poner 
en una celda hay un exit inmediatamente después para saltar hasta el final y poder seguir con la recursión sin pasar por el mostrar que hay al final. 
Sólo cuando no quedan ceros en la matriz termina la recorrida de la misma y el procedimiento la imprime.}

procedure resolverTablero;
    
var
	z,x,y:integer;
	
begin
	for x:=0 to 8 do
	begin
		for y:=0 to 8 do
		begin
			if sdk[x,y] = 0 then
			begin
			    for z:=1 to 9 do
			    begin
    				if not repetido(z,x,y) then
    				begin
    					sdk[x,y]:=z;
    					resolverTablero;
    					sdk[x,y]:=0;
    				end;
			    end;
			    exit;
			end;
		end;
	end;
	mostrarTablero;	
end;



BEGIN
	llenarTablero;
	mostrarTablero;
	writeln;
	resolverTablero;
END.
